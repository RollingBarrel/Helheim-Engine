#version 460 core

uniform layout(binding = 0, rgba32f) restrict image2D screenImage;
layout(binding = 0) uniform sampler2D bluredEmissive;
uniform float bloomIntensity;

layout(std140, binding = 0) uniform CameraMatrices
{
	mat4 view;
	mat4 proj;
	mat4 invView;
};
layout(binding = 1) uniform sampler2D depthTex;
layout(location = 1) uniform vec3 fogColor;
layout(location = 2) uniform float maxFog;
layout(location = 3) uniform float density;
layout(location = 4) uniform float heightFallof;

layout(binding = 2) uniform sampler2D volumetricTex;


float GetLinearZ(float inputDepth)
{
	return -proj[3][2] / (proj[2][2] + (inputDepth * 2.0 - 1.0));
}

vec3 GetWorldPos(float depth, vec2 uvs)
{
	float viewZ = GetLinearZ(depth);
	float viewX = (-viewZ * (uvs.x * 2.0 - 1.0)) / proj[0][0];
	float viewY = (-viewZ * (uvs.y * 2.0 - 1.0)) / proj[1][1];
	return (invView * vec4(vec3(viewX, viewY, viewZ), 1.0)).xyz;
}

//t = distance to point  // ro = cameraPos  // rd = camera-position vector
float ApplyFog(in float t, in vec3 ro, in vec3 rd)
{
	return (density / heightFallof) * exp(-ro.y * heightFallof) * (1.0 - exp(-t * rd.y * heightFallof)) / rd.y;
}

vec3 ACESFilm(in vec3 x)
{
	float a = 2.51f;
	float b = 0.03f;
	float c = 2.43f;
	float d = 0.59f;
	float e = 0.14f;
	return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
void main()
{
	ivec2 imgSize = imageSize(screenImage);
	if(gl_GlobalInvocationID.x < imgSize.x && gl_GlobalInvocationID.y < imgSize.y)
	{
		vec2 uv = vec2(gl_GlobalInvocationID.xy) / vec2(imgSize);
		vec4 screenColor = imageLoad(screenImage, ivec2(gl_GlobalInvocationID.xy));
		
		//volumetric
		screenColor.rgb += texture(volumetricTex, uv).rgb;
		
		//fog
		vec3 worldPos = GetWorldPos(texture(depthTex, uv).r, uv);
		vec3 cameraPos = vec3(invView[3][0], invView[3][1], invView[3][2]);
		vec3 rayDir = worldPos - cameraPos;
		float distance = length(rayDir);

		float fogAmount = min(ApplyFog(distance, cameraPos, rayDir / distance), maxFog);
		screenColor.rgb = (fogAmount * fogColor) + (screenColor.rgb * (1 - fogAmount));

		//bloom
		screenColor.rgb += texture(bluredEmissive, uv).rgb * bloomIntensity;

		//LDR color with reinhard tone Mapping
		//vec3 ldrCol = hdrCol / (hdrCol.rgb + vec3(1.0));
		//LDR color with ACES filmic tone Mapping
		vec3 ldrCol = ACESFilm(screenColor.rgb);

		//Gamma correction
		ldrCol = pow(ldrCol, vec3(1/2.2));

		//Output
		vec4 outColor = vec4(ldrCol, screenColor.a );	
		imageStore(screenImage, ivec2(gl_GlobalInvocationID.xy), outColor);
	}
}